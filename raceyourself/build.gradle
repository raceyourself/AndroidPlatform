buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        // Since Android's Gradle plugin 0.11, you have to use android-apt >= 1.3
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.+'
    }
}

apply plugin: 'android'
apply plugin: 'android-apt' // for clean integration of AndroidAnnotations

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
        }
    }
    defaultConfig {
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
        versionCode 1
        versionName '1.0'
    }
    signingConfigs {
        release {
            storeFile file("release-keystore.jks")
            storePassword "Running3141"
            keyAlias "RaceYourself Mobile"
            keyPassword "Running3141"
        }
    }
    buildTypes {
        debug {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-glassfit.pro', 'proguard-play-services.pro'
            maxHeapSize = "1024m"
        }
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-glassfit.pro', 'proguard-play-services.pro'
            signingConfig signingConfigs.release
            maxHeapSize = "1024m"
        }
    }
    productFlavors {
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

// required for AndroidAnnotations
apt {
    arguments {
        resourcePackageName "com.raceyourself.raceyourself" //android.defaultConfig.packageName
        androidManifestFile variant.processResources.manifestFile
    }
}

def AndroidAnnotationsVersion = '3.0.1'

dependencies {
    // Generates boilerplate Android code from annotations
    apt "org.androidannotations:androidannotations:$AndroidAnnotationsVersion"
    compile "org.androidannotations:androidannotations-api:$AndroidAnnotationsVersion"

    compile project(':platform')
    compile project(':facebook')

    // Magic fluency/syntactic sugar annotations. Using nightly build version instead in libs/ due to Mac incompatibility...
    //provided 'org.projectlombok:lombok:1.14.2'

    // New features on old versions of android
    compile 'com.android.support:appcompat-v7:19+'
    compile 'com.android.support:support-v13:19+'

    // Utilities - collections etc
    compile 'com.google.guava:guava:17.0'
//    compile 'net.tribe7.seeds:seeds-collect:+'

    compile 'com.nhaarman.listviewanimations:library:2.6.0'

    // dots/lines at bottom to indicate what page you're on
    compile 'com.viewpagerindicator:library:2.4.1@aar'

    // Horizontal scroll view
    compile 'it.sephiroth.android.library.horizontallistview:library:+'

    // Sticky list headers
    compile 'se.emilsjolander:stickylistheaders:2.4.+'

    // Logging facade
    compile 'org.slf4j:slf4j-api:1.7.6'
    // Logging implementation (point towards logcat via config)
    compile 'com.github.tony19:logback-android-core:1.1.1-2'
    compile 'com.github.tony19:logback-android-classic:1.1.1-2'

    //compile 'com.facebook:android-sdk:3.15.0'

    // Dates/times. Java Date/Calendar are horrible...
    compile 'joda-time:joda-time:2.+'

    // Image downloading and caching
    compile 'com.squareup.picasso:picasso:2.3.2'

    // For distance unit conversion - km-miles etc. Possibly overkill, but arguably cleaner code...
    //compile 'org.jscience:jscience:4.3.1'

    compile fileTree(dir: 'libs', include: ['*.jar'])

    // UX tracking / screen recording
    compile(name: 'UXCam', ext: 'aar')
}
